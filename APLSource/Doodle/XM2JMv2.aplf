 XM2JMv2←{
     ⍝ XML matrix to JSON matrix, no looping
     d e t a←↓4↑⍉⍵
     o←0<≢¨e                  ⍝ Row is an element/object
     n←o*⍨3+(≢¨a)+0<≢¨t       ⍝ Number of target JSON rows per source XML row
     s←0,+\¯1↓n               ⍝ Starting position
     i←o/s
     r←≢¨o/a                  ⍝ Number of attribute rows
     h←0<≢¨o/t                ⍝ Have a text item
     j←h/3+r+i                ⍝ Text indices
     c←1+⍳3
     m←0 '' '' 1⍴⍨4,⍨+/n
     m[1+i;c]←(⊂'Tag'),4,⍨⍪o/e
     m[2+∊i+⍳¨r;c]←4,⍨⊃⍪/a
     m[2+r+i;c]←(+/o)⌿⍉⍪'Content' '' 2
     m[j;1↓c]←4,⍨⍪h/o/t
     m[s/⍨~o;1↓c]←4,⍨⍪t/⍨~o
     m[;0]←(n/2×d)+(~m[;1]∊⊂'')+2×j∊⍨⍳≢m
     m
 }
